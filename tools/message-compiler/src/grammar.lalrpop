use crate::parsing::IdentifierRaw;
use crate::parsing::FieldRaw;
use crate::parsing::StructDefinitionRaw;
use crate::parsing::MetadataRaw;
use crate::parsing::FileRaw;
use crate::parsing::RpcDefinitionRaw;
use crate::parsing::RpcRaw;
grammar();

RIdentifier:IdentifierRaw<'input> =
    <id:r"[a-zA-Z0-9_][a-zA-Z0-9_-]*"> => IdentifierRaw::new(id);

RField:FieldRaw<'input> = <name:RIdentifier> ":" <type_name:RIdentifier> => FieldRaw::new(name, type_name);

RFields:Vec<FieldRaw<'input>> = {
    <field:RField> => vec![field],
    <mut rest:RFields> "," <field:RField?> => {
        if let Some(field) = field {
            rest.push(field);
        }

        rest
     }
}

RStructDefinition:StructDefinitionRaw<'input> = {
    "struct" <name:RIdentifier> "{" <fields:RFields?> "}" => StructDefinitionRaw(name, fields.unwrap_or_else(|| vec![])),
}

RMetadata:MetadataRaw<'input> = {
    "metadata" "{" <fields:RFields?> "}" => MetadataRaw::new(fields.unwrap_or_else(|| vec![]))
}

RRPCDefinition:RpcDefinitionRaw<'input> = {
    <name:RIdentifier> "(" <input_type:RIdentifier> ")" "->" <is_stream:"stream"?> <output_type:RIdentifier> => RpcDefinitionRaw::new(name, input_type, output_type, is_stream.is_some())
}

RRPCDefinitions:Vec<RpcDefinitionRaw<'input>> = {
    <rpc_def:RRPCDefinition> => vec![rpc_def],
    <mut rest:RRPCDefinitions> ";" <rpc_def:RRPCDefinition?> => {
        if let Some(rpc_def) = rpc_def {
            rest.push(rpc_def);
        }

        rest
    }
}

RRPC:RpcRaw<'input> = {
    "rpc" "{" <definitions:RRPCDefinitions?> "}" => RpcRaw::new(definitions.unwrap_or_else(|| vec![]))
}

RDefinitions:Vec<StructDefinitionRaw<'input>> = {
    <rest:RDefinitions?> <st:RStructDefinition> => {
        if let Some(mut rest) = rest {
            rest.push(st);

            rest
        } else {
            vec![st]
        }
    }
}

pub RFile:FileRaw<'input> = {
    <meta:RMetadata?> <definitions:RDefinitions> <rpc:RRPC?> => FileRaw::new(meta, definitions, rpc)
}
